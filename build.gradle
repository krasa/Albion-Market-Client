plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.11.4'

}


compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
test {
    useJUnitPlatform()
}
javafx {
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics']
}
if(JavaVersion.current() == JavaVersion.VERSION_15){
    throw new RuntimeException("JDK 15 is broken, use 14! https://bugs.openjdk.java.net/browse/JDK-8254920")
}

version = '0.3'

repositories {
    mavenCentral()
}
project.ext.buildDate = new Date()

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.3'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.springframework:spring-web:5.3.2'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.springframework.boot:spring-boot-starter:2.4.1'
    implementation 'org.openjfx:javafx-controls:11.0.2'
    implementation 'org.openjfx:javafx-fxml:11.0.2'
    implementation 'org.openjfx:javafx-media:11.0.2'
    implementation 'net.rgielen:javafx-weaver-spring:1.3.0'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.controlsfx:controlsfx:11.0.3'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools:2.4.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.4.1'
}


application {
    mainClassName = "krasa.albion.Launcher"
    applicationName = 'Albion Market Client'
}

//https://github.com/beryx-gist/badass-runtime-example-javafx
runtime {
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        jvmArgs = ['-Dlogback.configurationFile=conf/logback-spring.xml']
        noConsole = false
    }
    jpackage {                   
        installerType = 'msi'
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        imageOptions = ["--icon", "src/main/resources/krasa/albion/Client.ico"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'Nimblsoft']
        installerOptions += ['--verbose']
        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            installerOptions += ['--win-menu-group', 'Albion Market Client']
        }
    }
    
//   
//    targetPlatform("win") {
//        jdkHome = 'C:\\Users\\i7\\.jdks\\openjdk-15.0.1'
//    }
}
def imageConf = "$buildDir/jpackage/$rootProject.name/conf"
          
tasks.jpackageImage.doLast  {        
     copy{
        from('conf')
         into(imageConf)
    }         
    copy {
        from('src/main/resources/logback-spring.xml')
        into(imageConf)
    }
}